buildscript {
    ext.kotlin_version = "1.3.70"
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version"
    id 'maven-publish'
}

group 'com.harry1453.klaunchpad'
version '0.0.6'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    js {
        browser {
            dceTask {
                keep 'KLaunchpad.connectToLaunchpadMK2', 'KLaunchpad.connectToLaunchpadPro'
            }
            testTask {
                enabled = false
            }
        }
    }
    mingwX64("windows") {
        binaries {
            executable("KLaunchpad")
        }
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.ExperimentalStdlibApi')
                useExperimentalAnnotation('kotlin.contracts.ExperimentalContracts')
                useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes')
                useExperimentalAnnotation('kotlinx.coroutines.ExperimentalCoroutinesApi')
            }
        }

        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.4'
            }
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependsOn commonMain
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4"
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }

        jsMain {
            dependsOn commonMain
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.3.4"
            }
        }

        windowsMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.3.4"
            }
        }
    }
}

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = "klaunchpad-common"
        }
    }
}

task preparePublishNpm() {
    dependsOn clean
    dependsOn compileKotlinJs
    compileKotlinJs.mustRunAfter(clean)
    doLast {
        delete {
            "$projectDir/build/js/packages/KLaunchpad/package.json"
        }
        copy {
            from "$projectDir/src/jsMain/resources/package.json"
            into "$projectDir/build/js/packages/KLaunchpad/"
        }
        def path = "$projectDir/build/js/packages/KLaunchpad/".replace("\\", File.separator).replace("/", File.separator)
        println()
        println("Ready to publish! When you are ready to publish, enter the following commands:")
        println()
        println("cd \"$path\"")
        println("npm publish")
        println()
    }
}
