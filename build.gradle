plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.70'
}

group 'com.harry1453.klaunchpad'
version '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    js {
        browser {
            dceTask {
                // Keep APIs
                keep 'KLaunchpad.JSLaunchpad', 'KLaunchpad.Pad', 'KLaunchpad.Color'
            }
            webpackTask {
                output.libraryTarget = "commonjs"
            }
        }
    }
    mingwX64("windows") {
        binaries {
            executable("KLaunchpad")
        }
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.ExperimentalStdlibApi')
                useExperimentalAnnotation('kotlin.contracts.ExperimentalContracts')
                useExperimentalAnnotation('kotlin.ExperimentalUnsignedTypes')
                useExperimentalAnnotation('kotlinx.coroutines.ExperimentalCoroutinesApi')
            }
        }

        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.4'
            }
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependsOn commonMain
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4"
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }

        jsMain {
            dependsOn commonMain
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.3.4"
            }
        }

        windowsMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.3.4"
            }
        }
    }
}
